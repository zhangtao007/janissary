plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.8'
    // 就是这个插件
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

group 'lanjiu.janissary'
version '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	 maven{url 'http://maven.aliyun.com/nexus/content/groups/public/'}
   // mavenCentral()
}
dependencies {
    compile("io.netty:netty-all:4.1.10.Final",
            "com.google.protobuf:protobuf-java:3.10.0",
            "com.google.protobuf:protobuf-java-util:3.10.0",
            "org.slf4j:slf4j-api:1.7.10",
            "org.slf4j:slf4j-log4j12:1.7.10",
            'io.grpc:grpc-netty-shaded:1.24.0',
            'io.grpc:grpc-protobuf:1.24.0',
            'io.grpc:grpc-stub:1.24.0',
            'org.mybatis:mybatis:3.5.3',
            'com.alibaba:fastjson:1.2.30',
            'mysql:mysql-connector-java:5.1.49',
            'org.quartz-scheduler:quartz:2.3.0',
            'org.apache.commons:commons-pool2:2.5.0',
            'com.aliyun:dysmsapi20170525:2.0.1'

    )
    compile files('libs/aliyun-java-sdk-core-4.1.0.jar')
}


//jar {
//    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
//    manifest {
//
//        attributes 'Main-Class': "com.lanjiu.im.netty.server.LoginRegistCommunicationServer"
//    }
//}


//禁掉jar task
jar.enabled = false
// 执行shadow的打包
shadowJar {
    baseName = 'login-registration-communication'
    //classifier是生成jar包的后缀
    classifier = null
    version = '1.0'
    manifest {
        // 这里指定main class
        attributes 'Main-Class': 'com.lanjiu.im.netty.server.LoginRegistCommunicationServer'
    }
}
assemble.dependsOn(shadowJar)


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.24.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                setOutputSubDir 'java'
            }
        }
    }
    generatedFilesBaseDir = "src"
}
sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
        resources{
            srcDirs {
                [ "src/main/java","src/main/resource"]
            }
        }
    }
}




processResources {
    from('src/main/java') {
        include '**/*'      //导入里面的所有文件，也可以自定义正则表达式
        exclude "**/*.java" //源码文件中.java 文件是不需要的
    }
}